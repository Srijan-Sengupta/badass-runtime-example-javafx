plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.9'
    id 'org.beryx.runtime' version '1.11.4'
}

repositories {
    mavenCentral()
}

javafx {
    modules = ['javafx.controls', 'javafx.fxml']
}

application {
    mainClassName = "org.example.hellofx.Launcher"
    applicationName = 'hellofx'
}

task createSecondaryStartScripts(type: CreateStartScripts) {
    outputDir = tasks.startScripts.outputDir
    mainClass = 'org.example.hellofx.Launcher2'
    applicationName = 'hellofx2'
    classpath = tasks.startScripts.classpath
}

tasks.installDist.dependsOn tasks.createSecondaryStartScripts

tasks.jpackageImage.doFirst {
    def propFile = file("$buildDir/hellofx2.properties")
    propFile.delete()
    propFile.withOutputStream { it << """
        |main-jar=hellofx.jar
        |main-class=org.example.hellofx.Launcher2
    """.stripMargin()}
}

runtime {
    options = ['--strip-debug', '--compress', '2', '--no-header-files', '--no-man-pages']
    launcher {
        noConsole = true
    }
    jpackage {
        def currentOs = org.gradle.internal.os.OperatingSystem.current()
        def imgType = currentOs.windows ? 'ico' : currentOs.macOsX ? 'icns' : 'png'
        imageOptions += ['--icon', "src/main/resources/hellofx.$imgType"]
        imageOptions += ['--add-launcher', "hellofx2=$buildDir/hellofx2.properties"]
        installerOptions += ['--resource-dir', "src/main/resources"]
        installerOptions += ['--vendor', 'Acme Corporation']
        if(currentOs.windows) {
            installerOptions += ['--win-per-user-install', '--win-dir-chooser', '--win-menu', '--win-shortcut']
        }
    }
}
